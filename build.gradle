/*
 * Copyright 2010 the original author or authors.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

// -----------------------------------------------------------------------------
// Main gradle build file for Spring Social Instagram
// @author Matt Wright
// -----------------------------------------------------------------------------

// -----------------------------------------------------------------------------
// Configuration for the root project
// -----------------------------------------------------------------------------
description = 'Spring Social Instagram'

apply plugin: 'base'
apply plugin: 'idea'


def compatibilityVersion = 1.8

task wrapper(type: Wrapper) {
    group = 'Buildmaster'
    description = "Generates gradlew and gradlew.bat bootstrap scripts"
    gradleVersion = '1.9'
}

// -----------------------------------------------------------------------------
// Configuration for all projects including this one (the root project)
//
// @see settings.gradle for list of all subprojects
// -----------------------------------------------------------------------------
allprojects {
    // group will translate to groupId during pom generation and deployment
    group = 'org.springframework.social'

    // version will be used in maven pom generation as well as determining
    // where artifacts should be deployed, based on release type of snapshot,
    // milestone or release.
    // @see gradle.properties for the declaration of this property.
    version = springSocialInstagramVersion

    // default set of maven repositories to be used when resolving dependencies
    repositories {
        maven { url "http://repo.spring.io/libs-milestone" } 
        maven { url "http://repo.spring.io/libs-snapshot" } 
        mavenCentral()
    }
}


// -----------------------------------------------------------------------------
// Create collections of subprojects - each will receive their own configuration
// - all subprojects that start with spring-social-* are 'java projects'
// - documentation-related subprojects are not collected here
//
// @see configure(*) sections below
// -----------------------------------------------------------------------------

def javaprojects = subprojects.findAll { project ->
    project.path.startsWith(':spring-social-')
}

// -----------------------------------------------------------------------------
// Configuration for all java subprojects
// -----------------------------------------------------------------------------
configure(javaprojects) {

    apply plugin: 'java' // tasks for conventional java lifecycle
    apply plugin: 'maven' // `gradle install` to push jars to local .m2 cache
    apply plugin: 'eclipse' // `gradle eclipse` to generate .classpath/.project
    apply plugin: 'idea' // `gradle idea` to generate .ipr/.iml

    // add tasks for creating source jars and generating poms etc
    apply from: "${rootProject.projectDir}/publish-maven.gradle"
    
    ext {
        springSocialVersion = '2.0.0.BUILD-SNAPSHOT'
        jacksonVersion = '2.9.10'
        jspApiVersion = '2.3.1'
        junitVersion = '4.11'
        mockitoVersion = '1.9.5'
        servletApiVersion = '3.1.0'
        springVersion = '5.1.8.RELEASE'
    }

    sourceCompatibility = compatibilityVersion
    targetCompatibility = compatibilityVersion

    sourceSets {
        test {
            resources {
                srcDirs = ['src/test/java']
            }
        }
    }

    // dependencies that are common across all java projects
    dependencies {
        testCompile "junit:junit:$ext.junitVersion"
        testCompile "org.mockito:mockito-all:$ext.mockitoVersion"
        testCompile "org.springframework:spring-test:$ext.springVersion"
    }

    // enable all compiler warnings (GRADLE-1077)
    [compileJava, compileTestJava]*.options*.compilerArgs = ['-Xlint:all']
}


// -----------------------------------------------------------------------------
// Configuration for each individual core java subproject
//
// @see configure(javaprojects) above for general config
// -----------------------------------------------------------------------------

project('spring-social-instagram') {
    description = 'Instagram API'
    dependencies {
        compile "org.springframework.social:spring-social-core:$ext.springSocialVersion"
        compile ("org.springframework.social:spring-social-config:$springSocialVersion") 
        compile ("org.springframework.social:spring-social-security:$springSocialVersion", optional)               
        compile ("com.fasterxml.jackson.core:jackson-core:$ext.jacksonVersion")
        compile ("com.fasterxml.jackson.core:jackson-databind:$ext.jacksonVersion")
        compile ("com.fasterxml.jackson.core:jackson-annotations:$ext.jacksonVersion")
        compile ("javax.servlet:javax.servlet-api:$ext.servletApiVersion", provided)
        compile ("javax.servlet.jsp:javax.servlet.jsp-api:$ext.jspApiVersion", provided)
        testCompile "org.springframework.social:spring-social-test:1.0.0.RELEASE"
    }
}
